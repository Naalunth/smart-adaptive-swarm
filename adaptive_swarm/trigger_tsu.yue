-- Event Type: TSU
-- Check On... Every Frame

(allStates, ...) -> with aura_env
    -- return early to avoid processing too much
    if GetTime! <= .nextUpdate then return false

    getExpirationTime = (id) ->
        start, duration = GetSpellCooldown id
        start + duration
    swarmExpirationTime = getExpirationTime .SPELL_SWARM
    gcdExpirationTime = getExpirationTime .SPELL_GCD

    -- find the previous state
    previousState = nil
    for _, state in pairs(allStates)
        previousState = state
        break
    
    print "time: #{GetTime!}"
    changeMade = false

    -- highlight nothing if swarm isn't ready to be used
    if swarmExpirationTime == gcdExpirationTime
        .nextUpdate = GetTime! + .config.updateInterval

        swarmUnit = if .shouldCastOnFocus! then .focusTarget else .optimalAlly! ?? .lowestHealth!
        
        if swarmUnit?
            print "swarm unit: #{swarmUnit}"
            if swarmUnit == previousState?.unit
                previousState.changed = false
                return false

            print "adding new state (#{swarmUnit})"
            -- play the target sound
            sound = if .config.sound != .CONFIG_SOUND_NEVER and swarmUnit == .focusTarget then .SOUND_FOCUS
            -- or cooldown sound
            elseif .config.sound == .CONFIG_SOUND_ALL and not previousState? then .SOUND_COOLDOWN
            else .SOUND_NONE

            allStates[swarmUnit] = 
                show: true
                changed: true
                playSound: sound
                unit: swarmUnit
            changeMade = true

    else
        .nextUpdate = swarmExpirationTime
    
    if previousState? then
        print "removing previous state (#{previousState.unit})"
        previousState.show = false
        previousState.changed = true
        changeMade = true

    dump = (o) ->
        if type(o) == "table"
            local entries = for k, v in pairs o do "[#{k}] = #{dump(v)}"
            "{#{table.concat entries, ', '}}"
        else tostring o
    
    print "states: #{dump(allStates)}"

    return changeMade
